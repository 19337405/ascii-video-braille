// SETUP

offset = 0 // x offset
groups_in_use = 900 // how many groups you want to use, if it errors, you may want to lower the number
use_1x = false // slows down the loading speed by 50% and might also crash
size = 7 // 7 blocks tall

// END SETUP



if groups_in_use < 50 { throw "too little groups!" }
extract obj_props

$.print("reading output...")
__text = $.readfile("out.txt").split("|")[:-1].map(v => v.split(";"))

framerate = @number(__text[0][0])
width = @number(__text[0][1])
height = @number(__text[0][2])
text = __text[1:]

player_speed = 19.1846522782 if use_1x == false else 10.3761348898
frame_speed = player_speed*30*framerate

let groups = level.get_objects(GROUPS, g => true).map(o => o[GROUPS]).filter(n => n != null).flatten().length // p
let used_groups_idx = 0
let used_groups = []
let using_used_groups = false
let last_group = null

obj{
    OBJ_ID: 1334 if use_1x == false else 201,
    X: -15,
    Y: 15,
}.add()

BG.set(rgb(0, 0, 0))
1c.set(rgb(1, 0, 0))

if framerate == 0 {
    for j in 0..text.length {
        frame = text[j]
        for i in 0..frame.length/2 {
            let color = frame[i*2].split(",")
            color[0] = color[0].replace("\n", "").replace("\r", "")
            channel = frame[i*2+1]

            channel.to_obj().with(HVS_ENABLED, true).with(HVS, "{}a{}a{}a0a0".fmt(color)).with(X, 210).with(Y, 120).with(SCALING, ((width+0.5)/(height+0.5)*size)/width).add()
        }
    }
} else {
    $.print()
    for j in 0..text.length {
        frame = text[j]
        if frame.length < 2 {
            if last_group != null {
                obj{
                    OBJ_ID: 1049,
                    X: offset + (j+1)*frame_speed,
                    Y: -60,
                    TARGET: last_group,
                    ACTIVATE_GROUP: false,
                }.add()
            }

            $.print("\u{1b}[1Aframe ",j+1,"/",text.length)
            continue
        }

        if groups >= groups_in_use {
            using_used_groups = true
            groups = 0
            // loops++
        }

        let group = null
        if using_used_groups {
            if used_groups_idx >= groups_in_use {
                used_groups_idx = 0
                // loops++
            }
            group = used_groups[used_groups_idx]
            used_groups_idx++
        } else {
            group = ?g
            used_groups.push(group)
            groups++
        }
        
        obj{
            OBJ_ID: 1049,
            X: -30,
            Y: -30,
            TARGET: group,
            ACTIVATE_GROUP: false,
        }.add()

        for i in 0..frame.length/2 {
            let color = frame[i*2].split(",")
            color[0] = color[0].replace("\n", "").replace("\r", "")
            channel = frame[i*2+1]

            channel.to_obj().with(HVS_ENABLED, true).with(HVS, "{}a{}a{}a0a0".fmt(color)).with(X, 75+offset + j*frame_speed).with(Y, 150).with(SCALING, ((width+0.5)/(height+0.5)*size)/width).with(GROUPS, group).add()
        }

        obj{
            OBJ_ID: 901,
            X: offset + j*frame_speed-1,
            Y: -20,
            TARGET: group,
            DURATION: framerate*1.1,
            MOVE_X: frame_speed*1.1,
        }.add()

        obj{
            OBJ_ID: 1049,
            X: offset + j*frame_speed,
            Y: -40,
            TARGET: group,
            ACTIVATE_GROUP: true,
        }.add()

        obj{
            OBJ_ID: 1049,
            X: offset + (j+1)*frame_speed,
            Y: -60,
            TARGET: group,
            ACTIVATE_GROUP: false,
        }.add()

        last_group = group
        if j%3==0 {
            $.print("\u{1b}[1Aframe ",j+1,"/",text.length)
        }
    }
}